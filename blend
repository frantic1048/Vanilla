#!/usr/bin/env nu
use std log
use blend_util expand_package_dir

let self_dir = $env.FILE_PWD

let not_package_name = [
  "darwin-system" # nix-darwin
  "root" # linux system config, not managed by stow
  "screenshots" # screenshots
  ...(
    cd $self_dir;
    ls
    | where type == dir
    | get name
    | to text
    | git check-ignore --stdin
    | lines
    | each { path basename }
  )
]

let all_top_dirs: list<string> = (ls $self_dir
  | where type == dir
  | get name
  | path basename)

let all_packages: list<string> = ($all_top_dirs | where $it not-in $not_package_name)

let home_path = $nu.home-path
let macos_application_support_path = [$home_path "Library" "Application Support"] | path join
let macos_preferences_path = [$home_path "Library" "Preferences"] | path join
let xdg_config_home_path = [$home_path ".config"] | path join
def make_xdg_config_subpath [subpath: string] {
  [$xdg_config_home_path $subpath] | path join
}

let macos_stow_table: table<package: string, prefix: string> = [
  [package prefix];
  # shell and friends
  ["elvish" $xdg_config_home_path]
  ["nushell" $macos_application_support_path]
  ["starship" $xdg_config_home_path]
  ["pueue" $macos_application_support_path]

  # terminal
  ["alacritty" $xdg_config_home_path]
  ["kitty" $xdg_config_home_path]
  ["wezterm" $xdg_config_home_path]

  # desktop
  ["aerospace" $xdg_config_home_path]
  ["yabai" $xdg_config_home_path]
  ["skhd" $xdg_config_home_path]
  ["sketchybar" $xdg_config_home_path]

  # app
  ["bin" $home_path]
  ["git" $xdg_config_home_path]
  ["sapling" $macos_preferences_path] # https://sapling-scm.com/docs/introduction/getting-started#setting-your-identity
  ["proto" $home_path]
  ["vscode" ([$macos_application_support_path 'Code'] | path join)]
  ["vscode" ([$macos_application_support_path 'code-oss-dev'] | path join)]
  ["neofetch" $xdg_config_home_path]
  ["ncdu" $xdg_config_home_path]
  ["tealdeer" $macos_application_support_path]
]
let linux_stow_table: table<package: string, prefix: string> = [
  [package prefix];
  # shell and friends
  ["nushell" $xdg_config_home_path]
  ["elvish" $xdg_config_home_path]
  ["starship" $xdg_config_home_path]
  ["pueue" $xdg_config_home_path]

  # terminal
  ["alacritty" $xdg_config_home_path]
  ["tmux" $home_path]
  ["sakura" $xdg_config_home_path]

  # desktop
  ["alsa" $home_path]
  ["pipewire" $xdg_config_home_path]
  ["pulseaudio" $xdg_config_home_path]
  ["fontconfig" $xdg_config_home_path]
  ["pam_env" $home_path]
  # desktop:X11
  ["X" $home_path]
  ["i3wm" $xdg_config_home_path]
  ["picom" $xdg_config_home_path]
  ["tint2" $xdg_config_home_path]
  # desktop:Wayland
  ["sway" $xdg_config_home_path]
  ["waybar" $xdg_config_home_path]
  ["rofi" $xdg_config_home_path]
  ["mako" $xdg_config_home_path]

  # app
  ["bin" $home_path]
  ["git" $xdg_config_home_path]
  ["sapling" $xdg_config_home_path] # https://sapling-scm.com/docs/introduction/getting-started#setting-your-identity
  ["vscode" ([$xdg_config_home_path 'Code - OSS'] | path join)]
  ["htop" $xdg_config_home_path]
  ["nano" $xdg_config_home_path]
  ["sxiv" $xdg_config_home_path]
  ["swayshot" $xdg_config_home_path]
  ["fcitx" $xdg_config_home_path]
  ["npm" $home_path]
  ["makepkg" $home_path]
  ["neofetch" $xdg_config_home_path]
  ["ncdu" $xdg_config_home_path]
  ["tealdeer" $xdg_config_home_path]
]

let stow_profiles: table<name: string, table: table<package: string, prefix: string>> = [
  [name table];
  [macos-x86_64 $macos_stow_table]
  [macos-aarch64 $macos_stow_table]
  [linux-x86_64 $linux_stow_table]
]

def assert_stow_profile_exist [profile: string] {
  if $profile not-in ($stow_profiles | get name) {
    log error $"Error: profile ($profile) does not exist."
    exit 1
  }
}

def get_stow_profile [] -> table<name: string, table: table<package: string, prefix: string>> {
  log info "Getting stow profile..."
  let os_name = $nu | get os-info.name
  let os_arch = $nu | get os-info.arch
  let profile_name = $"($os_name)-($os_arch)"
  log info $"Expected profile: ($profile_name)"
  assert_stow_profile_exist $profile_name
  $stow_profiles | where name == $profile_name | first
}

def get_prefixes_of_package [
  profile: record<name: string, table: table<package: string, prefix: string>>,
  package: string
] -> list<string> {
  $profile.table | where package == $package | get prefix
}

def is_known_package [package: string] {
  $package in $all_packages
}

def assert_stow_profile_only_contains_known_items [profile: record<name: string, table: table<package: string, prefix: string>>] {
  let unknown_items = $profile | get table | where not (is_known_package $it.package)
  if ($unknown_items | length) > 0 {
    log error $"Error: in profile ($profile.name) found unknown items: ($unknown_items)"
    exit 1
  }
}

def verify [] {
  log info "Verifying stow list..."
  $stow_profiles | each {|row|
    log info $"Verifying stow list for ($row.name)..."
    assert_stow_profile_only_contains_known_items $row
  }
  log info "Stow list verified."
}

def provision_path [...paths: list<string>] {
  $paths | each {|path|
    if not ("dir" == ($path | path type)) {
      log info $"Creating path ($path)..."
      mkdir $path
    }
  }
}

def provision_essential_paths [] {
  log info "Provisioning essential paths..."
  provision_path $xdg_config_home_path
  log info $"Provisioned essential paths."
}

def init [] {
  provision_essential_paths
  log info $"All packages: ($all_packages)"
  verify
  log info "Initializing stow..."
  let profile = (get_stow_profile)
  log info $"Found profile: ($profile.name)"
}

def uninstall [packages: list<string>] {
  let profile = (get_stow_profile)
  if ($packages | length) > 0 {
    $packages | each {|package|
      if (not (is_known_package $package)) {
        log error $"Error: package ($package) is not known."
        exit 1
      }
      let prefixes = (get_prefixes_of_package $profile $package)
      $prefixes | each {|prefix|
        log info $"Unstowing ($package) at ($prefix)"
        provision_path $prefix
        leog info $"stow --dir ($self_dir) --target ($prefix) -D ($package)"
        stow --dir $self_dir --target $prefix -D $package
      }
    }
  } else {
    log info "No packages specified, aborting."
  }
}

def install [packages?: list<string>] {
  let profile = (get_stow_profile)
  if ($packages | length) > 0 {
    $packages | each {|package|
      if (not (is_known_package $package)) {
        log error $"Error: package ($package) is not known."
        exit 1
      }
      let prefixes = (get_prefixes_of_package $profile $package)
      $prefixes | each {|prefix|
        log info $"Stowing ($package) to ($prefix)"
        provision_path $prefix
        log info $"stow --dir ($self_dir) --target ($prefix) -S ($package)"
        stow --dir $self_dir --target $prefix -S $package
      }
    }
  } else {
    $profile.table | each {|row|
      log info $"Stowing ($row.package) to ($row.prefix)"
      log info $"stow --dir ($self_dir) --target ($row.prefix) -S ($row.package)"
      stow --dir $self_dir --target $row.prefix -S $row.package
    }
  }
}

# Uninstall package configs
def "main uninstall" [
  ...packages: string # packages to uninstall
] {
  init
  uninstall $packages
  ()
}

# Install package configs
def "main install" [
  ...packages: string # packages to install, default: install all available packages
  ] {
  init
  install $packages
  ()
}

# Initial self check
def "main init" [] {
  init
  ()
}

def stat [] {
  $all_packages | each {|package|
    let profiles_of_package = ($stow_profiles | each {|profile|
      if $package in ($profile.table | get package) {
        $profile.name
      }
    })
    [[package profiles]; [$package $profiles_of_package]]
  } | reduce {|it, acc| $it ++ $acc }
}

# Print packages and their profiles
def "main stat" [] {
  let stat_table = (stat)

  $stat_table | sort-by -r profiles | update profiles {|row|
    $row.profiles | str join ", "
  }
}

def "main stat_markdown" [] {
  let stat_table = (stat)

  $stat_table | sort-by -r profiles | update package {|row|
    let link_title = $row.package
    let link_url = (expand_package_dir $row.package)

    $"[($link_title)]\(($link_url)\)"
  } | update profiles {|row|
    $row.profiles | each { $"`($in)`" } | str join ", "
  } | to md --pretty
}

# Run blend self tests
def "main test" [] {
  log info "Running tests..."
  do {
    cd $self_dir
    ./blend.test.nu
  }
  log info "Tests passed."
}

# Generate README.md
def "main readme" [] {
  do {
    cd $self_dir
    ./README.md.nu
  }
}

# Upgrade system packages
def "main s" [] {
  do {
    cd $self_dir
    if ("macos" == ($nu | get os-info.name)) {
      ./upgrade_macos.nu
    } else {
      # TODO: add upgrade script for Arch
      log error "Unsupported OS."
    }
  }
}

def main [] {
  init
}